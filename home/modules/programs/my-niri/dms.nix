{
  pkgs,
  lib,
  config,
  ...
}:
let
  cfg = config.programs.my-niri.dms;
  filesToInclude = [
    # Files under `$XDG_CONFIG_HOME/niri/dms` to be included into the new config
    "alttab" # Please note that niri will throw an error if any of these files are missing.
    "binds"
    "colors"
    "cursor"
    "layout"
    "outputs"
    "wpblur"
  ];

  createFilesToInclude =
    let
      configDir = "~/.config/niri/dms";
      paths = builtins.map (f: "${configDir}/${f}.kdl") filesToInclude;
    in
    pkgs.writeShellScript "niri-dms-create" ''
      mkdir -p ${configDir}
      touch ${lib.concatStringsSep " " paths}
    '';
in
{
  options.programs.my-niri.dms = {
    backlight-device = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = null;
    };
    keyboard-backlight-device = lib.mkOption {
      type = lib.types.nullOr lib.types.str;
      default = null;
    };
  };
  config = lib.mkIf config.programs.my-niri.enable {
    systemd.user.services.niri-dms-setup = {
      Service.Type = "oneshot";
      Service.ExecStart = createFilesToInclude;
      Install.WantedBy = [ "niri.service" ];
      Unit.Before = [ "niri.service" ];
    };
    systemd.user.services.dms = {
      Unit.Before = [ "xdg-desktop-autostart.target" ];
    };
    programs.niri.settings.binds =
      with config.lib.niri.actions;
      {
        "Mod+Shift+Comma" = {
          hotkey-overlay.title = "Settings";
          action = spawn "dms" "ipc" "call" "settings" "focusOrToggle";
        };
        "Mod+M" = {
          hotkey-overlay.title = "Task Manager";
          action = spawn "dms" "ipc" "call" "processlist" "focusOrToggle";
        };
        "Mod+N" = {
          hotkey-overlay.title = "Notification Center";
          action = spawn "dms" "ipc" "call" "notifications" "toggle";
        };
        "Mod+Space" = {
          hotkey-overlay.title = "Application Launcher";
          action = spawn "dms" "ipc" "call" "spotlight" "toggle";
        };
        "Mod+Shift+V" = {
          hotkey-overlay.title = "Clipboard Manager";
          action = spawn "dms" "ipc" "call" "clipboard" "toggle";
        };
        "Mod+Y" = {
          hotkey-overlay.title = "Browse Wallpapers";
          action = spawn "dms" "ipc" "call" "dankdash" "wallpaper";
        };
        "Mod+Semicolon" = {
          hotkey-overlay.title = "Lock Screen";
          action = spawn "dms" "ipc" "call" "lock" "lock";
        };
        "XF86AudioLowerVolume" = {
          allow-when-locked = true;
          action = spawn "dms" "ipc" "call" "audio" "decrement" "3";
        };
        "XF86AudioMute" = {
          allow-when-locked = true;
          action = spawn "dms" "ipc" "call" "audio" "mute";
        };
        "XF86AudioRaiseVolume" = {
          allow-when-locked = true;
          action = spawn "dms" "ipc" "call" "audio" "increment" "3";
        };
      }
      // lib.optionalAttrs (cfg.backlight-device != null) {
        "XF86MonBrightnessDown" = {
          allow-when-locked = true;
          action = spawn "dms" "ipc" "call" "brightness" "decrement" "5" cfg.backlight-device;
        };
        "XF86MonBrightnessUp" = {
          allow-when-locked = true;
          action = spawn "dms" "ipc" "call" "brightness" "increment" "5" cfg.backlight-device;
        };
      }
      // lib.optionalAttrs (cfg.keyboard-backlight-device != null) {
        "Shift+XF86MonBrightnessDown" = {
          allow-when-locked = true;
          action = spawn "dms" "ipc" "call" "brightness" "decrement" "5" cfg.keyboard-backlight-device;
        };
        "Shift+XF86MonBrightnessUp" = {
          allow-when-locked = true;
          action = spawn "dms" "ipc" "call" "brightness" "increment" "5" cfg.keyboard-backlight-device;
        };
      };

    systemd.user.services.dms.Install.WantedBy = lib.mkForce [ "niri.service" ];
    systemd.user.services.dms.Unit.After = [ "niri.service" ];

    # Enable dynamic theming
    programs.kitty.settings.extraConfig = ''
      include dank-tabs.conf
      include dank-theme.conf
    '';

    programs.dank-material-shell = {
      enable = true;
      systemd = {
        enable = true;
        restartIfChanged = true;
      };
      niri.includes = {
        enable = true; # Enable config includes hack.
        override = true; # If disabled, DMS settings won't be prioritized over settings defined using niri-flake
        originalFileName = "hm"; # A new name (without extension) for the config file generated by niri-flake.
        inherit filesToInclude;
      };

      # Core features
      enableSystemMonitoring = true; # System monitoring widgets (dgop)
      enableDynamicTheming = true; # Wallpaper-based theming (matugen)
      enableAudioWavelength = true; # Audio visualizer (cava)
      enableCalendarEvents = false; # Calendar integration (khal)

      settings = builtins.fromJSON (builtins.readFile ./config.json);
    };
  };
}
